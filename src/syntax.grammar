@top Program { expression* }

@skip { whitespace | LineComment }

value { True | False | Null | Number | String | Record | List }

String { string }
Record { "{" list<Field>? "}" }
List  { "[" list<value>? "]" }

Field { FieldName ":" value }
FieldName { String | Identifier }

expression {
  Identifier |
  value |
  application { "(" expression* ")" }
}

@tokens {
  True  { "true" }
  False { "false" }
  Null  { "null" }

  Number { '-'? int frac? exp?  }
  int  { '0' | $[1-9] @digit* }
  frac { '.' @digit+ }
  exp  { $[eE] $[+\-]? @digit+ }

  string { '"' char* '"' }
  char { $[\u{20}\u{21}\u{23}-\u{5b}\u{5d}-\u{10ffff}] | "\\" esc }
  esc  { $["\\\/bfnrt] | "u" hex hex hex hex }
  hex  { $[0-9a-fA-F] }

  whitespace { $[ \n\r\t] }

  "{" "}" "[" "]"

  Identifier { $[a-zA-Z_\-0-9]+ }

  LineComment { "|" ![\n]* }

  "(" ")"

  @precedence { True, Identifier }
  @precedence { False, Identifier }
  @precedence { Null, Identifier }
  @precedence { Number, Identifier }
}

@detectDelim


list<item> { item ("," item)* }

@external propSource jsonHighlighting from "./highlight"

@detectDelim